# We're cross compiling!
set(CMAKE_CROSSCOMPILING 1)

# Calculate the generic tribble.
default_tc_value(XCMAKE_GENERIC_TRIBBLE "${XCMAKE_OS}-${XCMAKE_ARCH}-generic")

# Calculate the crosstool-NG template and the conventional target tuple.
default_tc_value(XCMAKE_TRIPLE_VENDOR unknown)
default_tc_value(XCMAKE_CTNG_VENDOR ${XCMAKE_TRIPLE_VENDOR})
if (XCMAKE_TRIPLE_ABI)
    set(XCMAKE_CTNG_SAMPLE "${XCMAKE_ARCH}-${XCMAKE_CTNG_VENDOR}-${XCMAKE_TRIPLE_OS}-${XCMAKE_TRIPLE_ABI}")
    set(XCMAKE_CONVENTIONAL_TRIPLE "${XCMAKE_ARCH}-${XCMAKE_TRIPLE_VENDOR}-${XCMAKE_TRIPLE_OS}-${XCMAKE_TRIPLE_ABI}")
else()
    set(XCMAKE_CTNG_SAMPLE "${XCMAKE_ARCH}-${XCMAKE_CTNG_VENDOR}-${XCMAKE_TRIPLE_OS}")
    set(XCMAKE_CONVENTIONAL_TRIPLE "${XCMAKE_ARCH}-${XCMAKE_TRIPLE_VENDOR}-${XCMAKE_TRIPLE_OS}")
endif()

# Set up Clang's target.
default_tc_value(CMAKE_C_COMPILER_TARGET "${XCMAKE_CONVENTIONAL_TRIPLE}")
default_tc_value(CMAKE_CXX_COMPILER_TARGET "${XCMAKE_CONVENTIONAL_TRIPLE}")

# Get the toolchain location.
default_tc_value(XCMAKE_TOOLCHAIN_BASE_DIR "/usr")
if (NOT DEFINED XCMAKE_TOOLCHAIN_DIR)
    set(XCMAKE_TOOLCHAIN_DIR "${XCMAKE_TOOLCHAIN_BASE_DIR}/${XCMAKE_TRIBBLE}")
    if (NOT EXISTS "${XCMAKE_TOOLCHAIN_DIR}")
        set(XCMAKE_TOOLCHAIN_DIR "${XCMAKE_TOOLCHAIN_BASE_DIR}/${XCMAKE_OS}-${XCMAKE_ARCH}-${XCMAKE_MICROARCH}")
    endif()
    if (NOT EXISTS "${XCMAKE_TOOLCHAIN_DIR}")
        set(XCMAKE_TOOLCHAIN_DIR "${XCMAKE_TOOLCHAIN_BASE_DIR}/${XCMAKE_OS}-${XCMAKE_ARCH}-generic")
    endif()
    if (NOT EXISTS "${XCMAKE_TOOLCHAIN_DIR}")
        message(FATAL_ERROR "Could not find toolchain for ${XCMAKE_TRIBBLE} in ${XCMAKE_TOOLCHAIN_BASE_DIR}")
    endif()
endif()

# Get find_package() and friends to look in the right place.
set(CMAKE_FIND_ROOT_PATH "${XCMAKE_TOOLCHAIN_DIR}" "${CMAKE_FIND_ROOT_PATH}")
set(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE only)
set(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY only)
set(CMAKE_FIND_ROOT_PATH_MODE_PACKAGE only)
set(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM never)

# Set up Clang's toolchain location.
default_tc_value(CMAKE_C_COMPILER_EXTERNAL_TOOLCHAIN "${XCMAKE_TOOLCHAIN_DIR}")
default_tc_value(CMAKE_CXX_COMPILER_EXTERNAL_TOOLCHAIN "${XCMAKE_TOOLCHAIN_DIR}")

# Set the sysroot location.
default_tc_value(CMAKE_SYSROOT "${XCMAKE_TOOLCHAIN_DIR}/${XCMAKE_CONVENTIONAL_TRIPLE}/sysroot")

# Set the linker to use.
set(XCMAKE_CLANG_LINKER_FLAGS "-fuse-ld=${XCMAKE_TOOLCHAIN_DIR}/bin/${XCMAKE_CONVENTIONAL_TRIPLE}-ld")
set(CMAKE_EXE_LINKER_FLAGS "${XCMAKE_CLANG_LINKER_FLAGS}")
set(CMAKE_MODULE_LINKER_FLAGS "${XCMAKE_CLANG_LINKER_FLAGS}")
set(CMAKE_SHARED_LINKER_FLAGS "${XCMAKE_CLANG_LINKER_FLAGS}")

# Set various CMake variables.
set(CMAKE_SYSTEM_PROCESSOR "${XCMAKE_ARCH}")
